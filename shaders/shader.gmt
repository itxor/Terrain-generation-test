#version 440 core
//матрица нормалей
uniform mat3 NormalMatrix;

//на вход - треугольника
layout(triangles) in;
//на выход - многоугольники из треугольников
layout(triangle_strip, max_vertices = 3) out;

//на вход - массив из 3-х векторов из шейдера оценки тесселяции
in vec3 tePosition[3]; 
//на вход - массив из 3-х векторов, описывающих текущие координаты
in vec3 tePatchDistance[3];

out vec3 gFacetNormal;
out vec3 gPatchDistance;
out vec3 gTriDistance;
 
void main()
{
    vec3 A = tePosition[2] - tePosition[0];
    vec3 B = tePosition[1] - tePosition[0];
    gFacetNormal = NormalMatrix * normalize(cross(A, B));
     
    gPatchDistance = tePatchDistance[0];
    gTriDistance = vec3(1, 0, 0);
    gl_Position = gl_in[0].gl_Position; 
    EmitVertex();
 
    gPatchDistance = tePatchDistance[1];
    gTriDistance = vec3(0, 1, 0);
    gl_Position = gl_in[1].gl_Position; 
    EmitVertex();
 
    gPatchDistance = tePatchDistance[2];
    gTriDistance = vec3(0, 0, 1);
    gl_Position = gl_in[2].gl_Position; 
    EmitVertex();
 
    EndPrimitive();
}